///$tab Main
SET ThousandSep=' ';
SET DecimalSep=',';
SET MoneyThousandSep=' ';
SET MoneyDecimalSep=',';
SET MoneyFormat='# ##0,00 ₽;-# ##0,00 ₽';
SET TimeFormat='h:mm:ss';
SET DateFormat='DD.MM.YYYY';
SET TimestampFormat='DD.MM.YYYY h:mm:ss[.fff]';
SET FirstWeekDay=0;
SET BrokenWeeks=1;
SET ReferenceDay=0;
SET FirstMonthOfYear=1;
SET CollationLocale='ru-RU';
SET CreateSearchIndexOnReload=1;
SET MonthNames='янв.;февр.;мар.;апр.;мая;июн.;июл.;авг.;сент.;окт.;нояб.;дек.';
SET LongMonthNames='январь;февраль;март;апрель;май;июнь;июль;август;сентябрь;октябрь;ноябрь;декабрь';
SET DayNames='пн;вт;ср;чт;пт;сб;вс';
SET LongDayNames='понедельник;вторник;среда;четверг;пятница;суббота;воскресенье';
SET NumericalAbbreviation='3:k;6:M;9:G;12:T;15:P;18:E;21:Z;24:Y;-3:m;-6:μ;-9:n;-12:p;-15:f;-18:a;-21:z;-24:y';

///$tab SA
SA:
LOAD
    "ACCESS" as ACCESS,
    '*' as SA_KEY,
    Upper("USERID") as USERID
FROM [lib://Users (rs360_qlikservice)]
(ooxml, embedded labels, table is Users)
Where ACCESS='ADMIN';

NoConcatenate

SA_tmp1:
LOAD "ACCESS" as ACCESS, 
     Upper("USERID") as USERID, 
     Text("TRADEMARK") as TRADEMARK
FROM [lib://Users (rs360_qlikservice)]
(ooxml, embedded labels, table is Users)
Where TRADEMARK<>'*' and ACCESS='USER'
;


Concatenate(SA)
LOAD
 ACCESS, 
 USERID, 
 Text(TRADEMARK) as SA_KEY
Resident SA_tmp1;
DROP Table SA_tmp1;

Section Access;
Auth:
LOAD
 ACCESS,
 USERID,
 SA_KEY
Resident SA
WHERE USERID <> '*';
Section Application;

NoConcatenate
///$tab Init
MasterCalendar_tmp:
Load * Inline [
	Наименование торговой марки,Дата
];


Users:
LOAD
    "ACCESS",
    TRADEMARK as "Наименование торговой марки",
    NAME,
    "USERID",
    DATEFROM
FROM [lib://Users (rs360_qlikservice)]
(ooxml, embedded labels, table is Users)
Where "ACCESS" = 'USER';


///$tab Master Calendar
LET usersRows = NoOfRows('Users');


for i=0 to $(usersRows) - 1

LET j = num(Peek('DATEFROM',$(i)));

trace $(j);

Do while j < num(today(1)) and Date(j) >= '01.01.2021'
	
      	Concatenate(MasterCalendar_tmp)
        Load
        	Text(Peek('Наименование торговой марки',$(i))) as "Наименование торговой марки",
            Date($(j)) as Дата
        AutoGenerate 1;
  LET j=j+1;

loop

Trace($(i));

next

Left Join(MasterCalendar_tmp)
LOAD
    Штрихкод,
    Text("Код продукта") as "Код продукта",
    "Наименование продукта",
    "Наименование торговой марки",
    "Наименование бренда",
    Отдел,
    Группа,
    Подгруппа
FROM [lib://Magnum Stage-2 (rs360_qlikservice)/Справочник товаров.qvd]
(qvd);

NoConcatenate

MasterCalendar:
Load
	Text("Код продукта") as "Код продукта",
    Штрихкод,
    "Наименование продукта",
    "Наименование торговой марки",
    "Наименование торговой марки" as SA_KEY,
    "Наименование бренда",
    Отдел,
    Группа,
    Подгруппа,
    Date([Дата]) as Дата,
    Week([Дата]) as Неделя,
    Month([Дата]) as Месяц,
    Year([Дата]) as Год,
    Date(Дата) & '|' & Text("Код продукта") as KEY
    
Resident MasterCalendar_tmp;

Drop Tables MasterCalendar_tmp,Users;


///$tab Sales
Data:
Load * Inline [
	ИД магазина
];

for Each vDir in DirList('lib://Magnum Stage-2 (rs360_qlikservice)/Продажи/*')
	for Each vDir2 in DirList(vDir & '/*')
    	for Each vFile in FileList(vDir2 & '/*.qvd')
          Concatenate(Data)
          LOAD
              Text("ИД магазина") as "ИД магазина",
              "Код поставщика",
              "Цена без НДС",
              "Цена с НДС",
              "Вид продажи",
              Количество,
              "Cумма без НДС",
              "Cумма с НДС",
              "Закупочная цена с НДС",
              Date("Дата покупки") & '|' & Text("Код продукта") as KEY
          FROM [$(vFile)] (qvd) Where Exists(Дата,Date("Дата покупки")) and Exists("Код продукта",Text("Код продукта"));
		Next vFile
	Next vDir2
Next vDir

Search Exclude KEY,"Закупочная цена с НДС","Cумма с НДС","Cумма без НДС",Количество,"Цена с НДС","Цена без НДС";







///$tab Suppliers Dictionary
Suppliers:
LOAD
        supp_code as [Код поставщика],
        FirstSortedValue(DISTINCT supp_name,-datestart) as [Наименование поставщика],
        Text(FirstSortedValue(DISTINCT code_zip,-datestart)) as [БИН поставщика]
    FROM [lib://Magnum Stage-1 (rs360_qlikservice)/ADB/dwh_data/dim_supp_list.qvd]
    (qvd)
    Group by
        supp_code;
///$tab Stores Dictionary
Stores:
LOAD
    "Населенный пункт",
    "Вид населенного пункта",
    Text("ИД магазина") as "ИД магазина",
    "Код магазина",
    Магазин,
    "Формат магазина",
    "Код типа подразделения",
    "Тип подразделения",
    "Адрес магазина"
FROM [lib://Magnum Stage-2 (rs360_qlikservice)/Справочник магазинов.qvd]
(qvd);


///$tab EXIT SCRIPT
exit script;
